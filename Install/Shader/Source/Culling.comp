#version 460

#include "Common.glsl"

layout (set = 0, binding = 0)  readonly buffer PerObjectBuffer
{
	PerObjectData objectDatas[];
};

layout (set = 1, binding = 0) writeonly buffer IndirectDraws
{
	IndexedIndirectCommand indirectDraws[];
};

layout (set = 2, binding = 0) writeonly buffer IndirectDrawCountBuffer
{
	IndirectDrawCount outDrawCount;
};

#define VIEW_DATA_SET 3
#include "ViewData.glsl"

#define FRAME_DATA_SET 4
#include "FrameData.glsl"

struct CullingPushConstants
{
	uint count;
};	

layout(push_constant) uniform constants
{   
   CullingPushConstants cullData;
};

void visibileCulling(uint id)
{
	PerObjectData objectData = objectDatas[id];

	vec3 localPos = objectData.sphereBounds.xyz;
	vec4 worldPos = objectData.modelMatrix * vec4(localPos, 1.0f);

	// local to world normal matrix.
	mat3 normalMatrix = transpose(inverse(mat3(objectData.modelMatrix)));
	mat3 world2Local = inverse(normalMatrix);

	bool bVisibile = true;

	// frustum test.
	for (int i = 0; i < 6; i++) 
	{
        vec3 worldSpaceN = viewData.frustumPlanes[i].xyz;
        float castDistance = dot(worldPos.xyz, worldSpaceN);

		// transfer to local matrix and use abs get first dimensions project value,
		// use that for test.
		vec3 localNormal = world2Local * worldSpaceN;
		float absDiff = dot(abs(localNormal), objectData.extents.xyz);
		if (castDistance + absDiff + viewData.frustumPlanes[i].w < 0.0)
		{
			bVisibile = false;
		}
	}
	
	// Sotre in buffer if visible
	if(bVisibile)
	{
		uint index = atomicAdd(outDrawCount.count, 1);

		indirectDraws[index].objectId      = id;
		indirectDraws[index].indexCount    = objectData.indexCount;
		indirectDraws[index].firstIndex    = objectData.firstIndex;
		indirectDraws[index].vertexOffset  = objectData.vertexOffset;
		indirectDraws[index].firstInstance = objectData.firstInstance;
		indirectDraws[index].instanceCount = 1;
	}
}

layout (local_size_x = 256) in;

void main()
{
    uint idx = gl_GlobalInvocationID.x;
	if (idx == 0)
	{
		atomicExchange(outDrawCount.count, 0);
	}

	groupMemoryBarrier();
    barrier();
    
    if(idx < cullData.count)
	{
		visibileCulling(idx);
    }
}